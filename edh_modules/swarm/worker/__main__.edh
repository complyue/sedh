#
# Swarm batch worker process entry module
#

import * 'net'

import * 'swarm/symbols'
import * 'swarm/compat'
import * 'swarm/RT'

# work definition scripts are allowed to change the inferred configuration at
# 'swarm/ENV', get & use the module reference so we always use up-to-date
# artifacts living there
senv = { import (** _ ) 'swarm/ENV' }


# take the worksource connection file describer, make it a peer for comm
peer = wscTake( senv.wscFd )
console.debug<| 'Took worker connection to worksource: ' ++ peer

jobSink = peer.armChannel( dataChan )

effect {
  ; @netPeer = peer
  ; @dataSink = jobSink
}


# inherit the work module, so we have access to `@doOneJob` implemented there
extends {
  import (** _ ) senv.jobWorkModu
}


# identify this connection as swarm worker to the headhunter, then it will
# start sending ips to our data channel
producer initiateJobs( outlet ) {
  peer.postCommand( expr

    StartWorking( {$ senv.swarmWorkerPid $}, {$ senv.swarmManagerPid $} )

  )
  console.debug<| 'Started working for worksource ' ++ peer
}

# apks are expected to be posted to a worker's data channel, one by one
go for ips from initiateJobs( outlet=jobSink ) do {
  console.debug<| 'Computing ips: ' ++ ips

  case super.@doOneJob(** ips ) of {
    { result } -> {
      console.debug<| 'Computed result ' ++ result ++ ' for ips: ' ++ ips
      peer.p2c( dataChan, repr( result ) )
    }
    throw UsageError( 'Swarm job returned no result', ips=ips )
  } $=> { jobExc } -> {
    console.error<| 'Swarm job [' ++ senv.jobWorkModu ++ ' - '
    ++ senv.swarmManagerPid ++ '>' ++ senv.swarmWorkerPid ++ '] failure:\n'
    ++ jobExc
    peer.p2c( errChan, repr( jobExc ) )
    break # stop processing more jobs
  }

}


{

  while peer.eol() is false case peer.readCommand() of {
    # sequence of commands each resulting in nil are expected and let pass here
    { cmdVal } -> {
      console.warn<| 'Unexpected cmd from ' ++ peer ++ '\n  ' ++ cmdVal
      cmdVal = nil # clear it
    }
  }

} @=> {
  { exc } -> {
    console.error<| 'Disconnecting worker from worksource ' ++ peer
    ++ ' for error: ' ++ exc
  }
  console.debug<| 'Disconnecting worker from worksource ' ++ peer
}
